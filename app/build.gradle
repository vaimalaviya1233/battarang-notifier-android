plugins {
  id 'com.android.application'
  id 'org.jetbrains.kotlin.android'
  id 'com.mikepenz.aboutlibraries.plugin'
}

import org.jetbrains.kotlin.gradle.dsl.JvmTarget

android {
  namespace = 'com.anissan.battarang'
  buildToolsVersion = '36.0.0'
  compileSdk = 36

  defaultConfig {
    targetSdk = 36
    minSdk = 21
    versionCode = 5 // Don't forget to also update the backend
    versionName = '1.7.0'

    // Generate "BuildConfig.*" properties
    Properties buildConfigProperties = new Properties()
    buildConfigProperties.load(new FileInputStream(rootProject.file('local.properties')))

    // Skip auto generated Gradle value
    buildConfigProperties.remove('sdk.dir')

    // Values loaded as characters; adding quotes to make them strings
    buildConfigProperties.each { key, value -> { buildConfigField('String', key, "\"$value\"") } }
  }

  buildTypes {
    debug {
      applicationIdSuffix = '.debug'
      versionNameSuffix = '-debug'
    }

    release {
      minifyEnabled = true
      shrinkResources = true
      proguardFiles(getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro')

      ndk {
        debugSymbolLevel = 'FULL'
      }
    }
  }

  compileOptions {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
  }

  kotlin {
    jvmToolchain(21)
    compilerOptions {
      jvmTarget = JvmTarget.JVM_21
    }
  }

  buildFeatures {
    buildConfig = true
    viewBinding = true
  }
}

dependencies {
  implementation 'org.jetbrains.kotlin:kotlin-stdlib:2.2.20'
  implementation 'org.jetbrains:annotations:26.0.2-1'

  implementation 'androidx.appcompat:appcompat:1.7.1'
  implementation 'androidx.core:core-ktx:1.17.0'
  implementation 'androidx.fragment:fragment-ktx:1.8.9'
  implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
  implementation 'androidx.viewpager2:viewpager2:1.1.0'
  //noinspection GradleDependency Higher versions include breaking changes that require re-theming
  implementation 'com.google.android.material:material:1.12.0'

  // `com.google.common.util.concurrent.ListenableFuture` imported in PermissionTabViewHolder
  implementation 'com.google.guava:guava:33.4.8-android'

  implementation 'io.insert-koin:koin-android:4.1.1'
  implementation 'com.squareup.okhttp3:okhttp:5.1.0'
  implementation 'io.github.g00fy2.quickie:quickie-bundled:1.11.0'
  implementation 'dev.chrisbanes.insetter:insetter:0.6.1'
  implementation 'com.github.judemanutd:autostarter:1.1.0'
  implementation 'com.mikepenz:aboutlibraries:12.2.4'
  implementation 'hu.autsoft:krate:2.0.0'

  // This snapshot version can set the app name for Doki API to use in instructions
  implementation 'dev.doubledot.doki:library:c18efeb'
}
